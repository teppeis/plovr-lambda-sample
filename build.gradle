plugins {
    id 'java'
    id 'io.spring.dependency-management' version '1.0.5.RELEASE'
    id 'de.undercouch.download' version '3.4.3'
}

group 'example'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8
def externalLibs = 'build/external-libs'
def plovrJar = "${externalLibs}/plovr.jar"

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'com.amazonaws:aws-java-sdk-bom:1.11.362'
    }
}

dependencies {
    compile(
            'com.amazonaws:aws-lambda-java-core:1.2.0',
            'com.amazonaws:aws-lambda-java-events:2.2.2',
            'com.amazonaws:aws-java-sdk-s3',
            'org.apache.commons:commons-compress:1.17',
            'com.fasterxml.jackson.core:jackson-databind:2.9.6',
            files(plovrJar),
    )
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtime
    }
}

task downloadExternalLibs(type: Download) {
    src 'https://github.com/teppeis/plovr/releases/download/v8.0.0-rc.1-reduce-gwt/plovr.jar'
    dest {
        new File(plovrJar).getParentFile().mkdirs()
        return plovrJar
    }
    onlyIfModified true
}

compileJava.dependsOn downloadExternalLibs
buildZip.dependsOn downloadExternalLibs
build.dependsOn buildZip
